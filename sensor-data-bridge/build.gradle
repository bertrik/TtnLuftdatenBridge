apply plugin: "application"

mainClassName = 'nl.bertriksikken.loraforwarder.SensorDataBridge'

dependencies {
    implementation project(":cayenne")

    implementation libs.slf4j.api
    implementation libs.slf4j.reload4j

    implementation libs.paho.client.mqttv3
    implementation libs.bundles.jackson
    
    // REST server
    implementation libs.bundles.jersey
    implementation libs.bundles.jetty
    // REST client
    implementation libs.bundles.retrofit
    
    compileOnly libs.spotbugs.annotations
    
    testImplementation libs.retrofit.mock
}

// fix scripts executable bit and line endings
import org.apache.tools.ant.filters.FixCrLfFilter

distributions {
    main {
        contents {
            filesMatching("**/*.sh") {
                mode = 0755
                filter(FixCrLfFilter,
                    eol: FixCrLfFilter.CrLf.newInstance('lf'),
                    tab: FixCrLfFilter.AddAsisRemove.newInstance('asis'),
                    eof: FixCrLfFilter.AddAsisRemove.newInstance('remove'),
                    fixlast: true)
            }
        } 
    }
}

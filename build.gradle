plugins {
    alias libs.plugins.errorprone
    alias libs.plugins.versions
    alias libs.plugins.dependencycheck
    alias libs.plugins.git.version
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyCheck {
    scanConfigurations = ['runtimeClasspath']
}

subprojects {
    apply plugin: "java"
    apply plugin: "eclipse"
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'com.github.ben-manes.versions'

    java {
        targetCompatibility = JavaVersion.VERSION_17
        sourceCompatibility = JavaVersion.VERSION_17
    }

    repositories {
        mavenCentral()
    }

    // Reject all non stable versions in dependency check
    tasks.named("dependencyUpdates").configure {
        rejectVersionIf {
            isNonStable(it.candidate.version)
        }
    }

    // Standard libraries added to all projects
    dependencies {
        errorprone("com.google.errorprone:error_prone_core:2.31.0")

        annotationProcessor libs.errorprone.annotationprocessor
        implementation libs.errorprone.annotations
        implementation libs.slf4j.api

        testAnnotationProcessor libs.errorprone.annotationprocessor
        testImplementation libs.junit
        testImplementation libs.mockito.core
    }

}
